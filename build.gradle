apply plugin: "groovy"
apply plugin: "maven"
apply plugin: 'signing'    

group = "org.grails"
version = "1.0.1"

configurations {
    deployerJars
    optional
}

repositories {
    mavenCentral()
}

dependencies {
    def groovyDep = "org.codehaus.groovy:groovy:1.7.6"

    groovy groovyDep

    compile  "org.slf4j:jcl-over-slf4j:1.6.1"

    optional "oro:oro:2.0.8", "picocontainer:picocontainer:1.2", groovyDep

    testCompile "junit:junit:3.8.2", "jmock:jmock:1.2.0", "junitperf:junitperf:1.8", groovyDep
}

sourceSets {
    main {
        compileClasspath += configurations.optional
    }
    test {
        runtimeClasspath += configurations.optional
    }
}

test.excludes = ["**/*Support.class"]

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    description = "Does a maven deploy of archives artifacts"

    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment ->
            signing.signPom(deployment)
        }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: project.hasProperty("sonatypeUsername") ? project.sonatypeUsername : null,
                    password: project.hasProperty("sonatypePassword") ?  project.sonatypePassword : null)
        }
        snapshotRepository(url: "http://repo.grails.org/grails/libs-snapshots-local") {
            authentication(userName: project.hasProperty("artifactoryPublishUsername") ? project.artifactoryPublishUsername : null,
                    password: project.hasProperty("artifactoryPublishPassword") ?  project.artifactoryPublishPassword : null)
        }

        pom.project {
            name 'Grails Doc Engine'
            packaging 'jar'
            description 'Documentation engine used to generate Grails docs'
            delegate.url 'http://grails.org/'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                delegate.url 'scm:git@github.com:grails/grails-gdoc-engine.git'
                connection 'scm:git@github.com:grails/grails-gdoc-engine.git'
                developerConnection 'scm:git@github.com:grails/grails-gdoc-engine.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    delegate.url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'alkemist'
                    name 'Luke Daley'
                }
                
                developer {
                    id 'graemerocher'
                    name 'Graeme Rocher'
                }
                developer {
                        id 'pledbrook'
                        name 'Peter Ledbrook'
                }                    
            }
        }
    }
}
